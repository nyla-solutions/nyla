apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'findbugs'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "com.github.nyla-solutions"
archivesBaseName = "nyla.solutions.commas.file"

version = '0.0.1'
jar {
    manifest {
        attributes 'Implementation-Title': 'Nyla Commas File',
                   'Implementation-Version': version
    }
}

//Solution by the first link
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

javadoc {
	source = sourceSets.main.allJava
	classpath = configurations.compile
	}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}


artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}


repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
	  compile group: 'com.github.nyla-solutions', name: 'nyla.solutions.core', version: '0.2'
	  compile group: 'com.github.nyla-solutions', name: 'nyla.solutions.commas', version: '0.1'
      testCompile group: 'junit', name: 'junit', version: '4.12'
}

test {
    systemProperties 'property': 'value'
}

uploadArchives {
	  repositories {
		  mavenDeployer {
		      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

		      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
		        authentication(userName: ossrhUsername, password: ossrhPassword)
		      }


	      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
	        authentication(userName: ossrhUsername, password: ossrhPassword)
	      }

	      pom.project {
	        name 'Nyla Commas File'
	        packaging 'jar'
	        // optionally artifactId can be defined here 
	        description 'Core utility features.'
	        url 'https://github.com/nyla-solutions/nyla/tree/master/nyla.solutions.commas.file'
	        
	        scm {
	            url 'https://github.com/nyla-solutions/nyla.git'
	          }
	        
	        
	        licenses {
	          license {
	            name 'The Apache License, Version 2.0'
	            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
	          }
	        }

	        developers {
	          developer {
	            id 'ggreen'
	            name 'Gregory Green'
	            email 'ggreen@g-solution.net'
	          }
	        }
	      }
	    }
	  }
	}
